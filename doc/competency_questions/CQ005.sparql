PREFIX wb: <http://hercules-demo.wiki.opencura.com/entity/>
PREFIX wbt: <http://hercules-demo.wiki.opencura.com/prop/direct/>

SELECT DISTINCT ?projectLabel ?propLabel ?oLabel WHERE {
  ?org wbt:P21 ?researchLine ;                                                        # get entities with property hasResearchLine
       wbt:P1 ?orgType ;
       rdfs:label ?orgLabel .
  ?researchLine wbt:P22 ?researchLineCode .                                           # get code of researchLine
  ?researchLineCode rdfs:label ?researchLineCodeLabel .
  FILTER(?orgType IN (wb:Q68, wb:Q2) &&                                               # organization is either research group or research institute
         ?orgLabel = "Basque Centre for Climate Change"@en &&                         # name of organization to be parameterized
         ?researchLineCodeLabel = "CTCLIM"@en)
  ?org wbt:P27 ?project .                                                             # get all projects of research center
  ?project wbt:P31 wb:Q158 ;                                                          # which are H2020
           ?p ?o .                                                                    # extract all predicates about the project
  ?prop wikibase:directClaim ?p .
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en,es" .
  }
}
ORDER BY ?projectLabel
