PREFIX p: <http://hercules-demo.wiki.opencura.com/prop/>
PREFIX pq: <http://hercules-demo.wiki.opencura.com/prop/qualifier/>
PREFIX ps: <http://hercules-demo.wiki.opencura.com/prop/statement/>
PREFIX wb: <http://hercules-demo.wiki.opencura.com/entity/>
PREFIX wbt: <http://hercules-demo.wiki.opencura.com/prop/direct/>

SELECT DISTINCT ?researcherLabel ?numberOfCitations ?hIndex ?i10Index WHERE {
  ?researcher wbt:P17 wb:Q44 . # get entities of role Researching
  ?researcher rdfs:label ?researcherLabel .
  FILTER(?researcherLabel = "Jos√© Emilio Labra Gayo"@es) # researcher to be parametrized
  ?researcher p:P39 [
                      ps:P39 ?numberOfCitations ;
                      pq:P44 ?citeDate
                    ] ;
              p:P40 [
                      ps:P40 ?hIndex ;
                      pq:P44 ?hIndexDate
                    ] ;
              p:P41 [
                      ps:P41 ?i10Index ;
                      pq:P44 ?i10Date
                    ] .


  FILTER(year(?citeDate) = 2020 && year(?hIndexDate) = 2020 && year(?i10Date) = 2020)
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en,es" .
  }
}
