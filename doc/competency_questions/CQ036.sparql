PREFIX wb: <http://hercules-demo.wiki.opencura.com/entity/>
PREFIX wbt: <http://hercules-demo.wiki.opencura.com/prop/direct/>

SELECT ?universityLabel (SUM(?projectBudget) AS ?totalProjectBudget) WHERE {
  ?project wbt:P1 ?projectType ;    # get all projects
           wbt:P46 ?projectBudget ;    # with a budget
           wbt:P32 ?researchField . # and research field
  ?researchField wbt:P23* ?researchFieldLevel1 .
  ?researchFieldLevel1 wbt:P24 wb:Q162 .  # get Level 1 Subject Area from the project
  FILTER(?projectType IN (wb:Q94, wb:Q155, wb:Q156, wb:Q157) &&   # types of projects to be parametrized
         ?researchFieldLevel1 = wb:Q189)   # subject area to be parametrized (in this case, Mathematical, physical, chemical and engineering sciences)

  ?researchCenter wbt:P27 ?project ; # get research centers participants on the project
                  wbt:P34 ?university . # and their associated university
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en,es" .
  }
}
GROUP BY ?universityLabel
ORDER BY DESC(SUM(?projectBudget))
