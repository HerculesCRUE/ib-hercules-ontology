PREFIX wb: <http://hercules-demo.wiki.opencura.com/entity/>
PREFIX wbt: <http://hercules-demo.wiki.opencura.com/prop/direct/>

SELECT DISTINCT ?projectLabel ?propLabel ?oLabel WHERE {
  ?org wbt:P21 ?researchLine ;                                                        # get entities with property hasResearchLine
       wbt:P1 ?orgType ;
       rdfs:label ?orgLabel .
  FILTER(?orgType IN (wb:Q68, wb:Q2) &&                                               # organization is either research group or research institute
         ?orgLabel = "Centre for Research in Mathematics"@en)                         # name of organization to be parameterized
  ?org wbt:P27 ?project .                                                             # get all projects of research center
  ?project wbt:P53 wb:Q315 .                                                          # which are competitive calls
  ?project ?p ?o .                                                                    # extract all predicates about the project
  ?prop wikibase:directClaim ?p .
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en,es" .
  }
}
ORDER BY ?projectLabel
