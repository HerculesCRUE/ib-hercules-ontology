PREFIX wb: <http://hercules-demo.wiki.opencura.com/entity/>
PREFIX wbt: <http://hercules-demo.wiki.opencura.com/prop/direct/>

SELECT DISTINCT ?projectLabel ?universityALabel ?universityBLabel ?projectBudget WHERE {
  ?project wbt:P1 ?projectType ;         # get all projects
           wbt:P46 ?projectBudget .      # with a budget
  FILTER(?projectType IN (wb:Q156, wb:Q155)) # national or european projects
  ?groupA wbt:P27 ?project ;
          wbt:P34 ?universityA .
  ?groupB wbt:P27 ?project ;
          wbt:P34 ?universityB .

  FILTER(?universityA != ?universityB)
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en,es" .
  }
}
ORDER BY ?projectLabel
