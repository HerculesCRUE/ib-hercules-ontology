PREFIX wb: <http://hercules-demo.wiki.opencura.com/entity/>
PREFIX wbt: <http://hercules-demo.wiki.opencura.com/prop/direct/>

SELECT DISTINCT ?authorLabel ?publicationLabel ?publisherLabel (year(?publicationDate) as ?publicationYear) WHERE {
  ?author wbt:P17 wb:Q44 ;               # get entities of role Researching
          wbt:P11 ?publication .         # with publications
  ?publication wbt:P1 ?publicationType .
  ?publicationType (wbt:P4)* wb:Q47 .    # publications that are subclasses of Publication (Q47)
  OPTIONAL {
    ?publication wbt:P14 ?publicationDate ;
                 wbt:P9 ?publisher .
  }
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "[AUTO_LANGUAGE],es" .
  }
}
ORDER BY ASC(?publicationLabel)
